#gamestart
command /start:
	permission: siso
	trigger:
		if {mv_bug_stop} is true:
			send "&cゲームを開始できませんでした (mv bug stopper)" to ops
			stop
		set {game} to true
		broadcast "&aゲームを開始します"
		command "/gamemode 0 @a"
		command "clear @a"
		register_scoreborad()
		glowing_block({map_setting::core::red}, "red")
		glowing_block({map_setting::core::blue}, "blue")
		random_team()
		loop all players:
			if {red::*} contain loop-player:
				command "/teamjoin %loop-player% red"
			if {blue::*} contain loop-player:
				command "/teamjoin %loop-player% blue"
		wait 1 tick
		loop all players:
			set gamemode of loop-player to spectator
			teleport loop-player to {map_setting::spawn_core}
			send title "&6MAP: &a&l%{last_map}%" to loop-player for 3 seconds
			sound(loop-player, "block.end_portal.spawn")
		wait 6 seconds
		loop all players:
			send title "&6&lGO!!" to loop-player for 2 seconds
			set gamemode of loop-player to survival
			command "/respawn %loop-player%"
			sound(loop-player, "entity.item.break")
		spawn_core()
		bossbar_timer({settings.time})

on respawn:
	wait a tick
	send subtitle "&3&lリスポーン中..." to player for 5 seconds
	teleport player to block 30 meters above {map_setting::spawn_core}
	set gamemode of player to spectator
	wait 6 seconds
	set gamemode of player to survival
	command "/respawn %player%"

command /respawn <player>:
	permission: siso
	trigger:
		clear arg 1's inventory
		heal arg 1
		set the arg 1's food level to 10
		command "/kit_item %arg 1% %{kit.%arg 1%}%"
		if {kit.%arg 1%} is not set:
			command "/kit_item %arg 1% knight"
		if {red::*} contain arg 1:
			teleport arg 1 to random location of {map_setting::spawn::red::*}
		if {blue::*} contain arg 1:
			teleport arg 1 to random location of {map_setting::spawn::blue::*}
		if {mode} is "bow":
			set slot 0 of arg 1's inventory to bow of power 30
			set slot 8 of arg 1's inventory to 64 arrow

command /teamjoin <player> <text>:
	permission: siso
	trigger:
		if arg 2 is "red":
			command "/scoreboard teams join red %arg 1%"
			set the arg 1's tablist name to "&c%arg 1%"
			set arg 1's nametag to "&c%arg 1%"
		if arg 2 is "blue":
			command "/scoreboard teams join blue %arg 1%"
			set the arg 1's tablist name to "&9%arg 1%"
			set arg 1's nametag to "&9%arg 1%"
		command "/respawn %arg 1%"
		command "/getkit %arg 1%"
		count_scoreboard(arg 1)

function random_team():
	set {_allplayers::*} to all players
	set {join::*} to all players
	loop size of {_allplayers::*} / 2 times:
		set {_random_player} to random player of {_allplayers::*}
		add {_random_player} to {red::*}
		remove {_random_player} from {_allplayers::*}
	set {blue::*} to {_allplayers::*}
	broadcast "DEBUG %{red::*}%"
	broadcast "DEBUG %{blue::*}%"

function register_scoreborad():
	command "/scoreboard teams add red"
	command "/scoreboard teams option red color red"
	command "/scoreboard teams option red friendlyfire false"
	command "/scoreboard teams option red nametagVisibility hideForOtherTeams"
	command "/scoreboard teams option red collisionRule pushOwnTeam"
	command "/scoreboard teams option red seeFriendlyInvisibles true"
	#blue
	command "/scoreboard teams add blue"
	command "/scoreboard teams option blue color blue"
	command "/scoreboard teams option blue friendlyfire false"
	command "/scoreboard teams option blue nametagVisibility hideForOtherTeams"
	command "/scoreboard teams option blue collisionRule pushOwnTeam"
	command "/scoreboard teams option blue seeFriendlyInvisibles true"

#---Item---
on rightclick holding nether star:
	name of held item contain "Team Join"
	command "/gamemode 0 %player%"
	command "clear %player%"
	set {_team} to auto_team_select()
	if {_team} is "red":
		add player to {red::*}
		command "/teamjoin %player% red"
	if {_team} is "blue":
		add player to {blue::*}
		command "/teamjoin %player% blue"
	add player to {join::*}

on rightclick holding emerald:
	name of held item contain "SHOP"
	command "/kit_shop %player%"

on rightclick holding chest:
	name of held item contain "Select Kit"
	command "/select_kit %player%"

function timeup():
	#レッドが勝っていたら
	if {max_count_red} < {max_count_blue}:
		#しかしブルーがコアを持っていたら延長戦
		if {blue::*} contain {has_core}:
			game_extension()
		else:
			win("red")
		stop
	#ブルーが勝っていたら
	if {max_count_blue} < {max_count_red}:
		#しかしレッドがコアを持っていたら延長戦
		if {red::*} contain {has_core}:
			game_extension()
		else:
			win("blue")
		stop
	broadcast "&6&l引き分け！！"
	reset()
	stop

function game_extension():
	send title "&6&l延長中!!" to all players for 1 seconds
	loop all players:
		sound(loop-player, "entity.zombie_villager.cure", 2)
	set {game_extension} to true
	stop

function win(color: text):
	set {ending} to true
	loop all players:
		send title "&6&lFinish!!" to loop-player for 3 seconds
		sound(loop-player, "entity.enderdragon.ambient")
		set gamemode of loop-player to spectator
	wait 4 seconds
	loop all players:
		sound(loop-player, "entity.player.levelup")
	if {_color} is "red":
		send title "&c&lRED &a&lWIN!!" to all players for 3 seconds
	if {_color} is "blue":
		send title "&9&lBLUE &a&lWIN!!" to all players for 3 seconds
	wait 4 seconds
	reset()

command /stopgame:
	permission: siso
	trigger:
		reset()

function reset():
	if {mv_bug_stop} is true:
		send "&cゲームをリセットできませんでした (mv bug stopper)" to ops
		stop
	set {game} to false
	set {ending} to false
	set {game_extension} to false
	set {already_first_get} to false
	set {dropped_core} to true
	set {max_count_red} to 100
	set {max_count_blue} to 100
	loop all players:
		teleport loop-player to {lobby}
		set gamemode of loop-player to adventure
		clear loop-player's inventory
		heal loop-player
		set food level of loop-player to 10
		reset loop-player's tablist name
		reset loop-player's nametag
		set glowing of loop-player to false
		set level of loop-player to 0
		set level progress of loop-player to 0
		give emerald named "&6SHOP" to loop-player
		give chest named "&6Select Kit" to loop-player
		set slot 8 of loop-player's inventory to type of writable book named "&dフィードバック" with lore "&9&l著名すると運営に送信されます"
		wipe loop-player's sidebar
	command "/effect @a clear"
	delete {red::*}
	delete {blue::*}
	delete {join::*}
	command "/scoreboard teams remove red"
	command "/scoreboard teams remove blue"
	map_create()
	if {auto_start} is true:
		broadcast "&630秒後に試合を開始します！"
		wait 30 seconds
		#二人以下だったら
		if size of all players < 2:
			broadcast "&cプレイヤーが足りないのでゲームを開始できませんでした"
			reset()
			stop
		command "/start"

on join:
	broadcast "&6%player% has join"
	if {game} is true:
		#add player to boss bar {bar}
		if {join::*} contain player:
			if {red::*} contain player:
				send "&c試合に戻ります"
				teleport player to {map_setting::spawn::red::1}
				set the player's tablist name to "&c%player%"
				set player's nametag to "&c%player%"
			if {blue::*} contain player:
				send "&c試合に戻ります"
				teleport player to {map_setting::spawn::blue::1}
				set the player's tablist name to "&9%player%"
				set player's nametag to "&9%player%"
		else:
			wait 2 ticks
			teleport player to {lobby}
			set gamemode of player to adventure
			clear player's inventory
			give nether star named "&6Team join" to player
	else:
		wait 2 ticks
		teleport player to {lobby}
		set gamemode of player to adventure
		clear player's inventory
		if {game} is true:
			give nether star named "&6Team Join" to player

on quit:
	set quit message to ""
	if {has_core} is player:
		remove all nether stars from player's inventory
		spawn_core()
	if {red::*} contain player:
		remove player from {red::*}
	if {blue::*} contain player:
		remove player from {blue::*}
	remove player from {join::*}